JZtxtcmd.bat %0 %1 %2

exit /B

==JZtxtcmd==

##jzTc script to generate a Reflection.crefl file from some struct of Header.
##made by Hartmut Schorrig,  



Map reflSimpleTypes = {
  String int = "REFLECTION_int32";
  String char = "REFLECTION_uint8";
  String uint32_t = "REFLECTION_uint32";
  String uint32 =   "REFLECTION_uint32";
  String int32_t =  "REFLECTION_int32";
  String int32 =    "REFLECTION_int32";
  String uint16_t = "REFLECTION_uint16";
  String Uint16 =   "REFLECTION_uint16";
  String int16_t =  "REFLECTION_int16";
  String int16 =    "REFLECTION_int16";
  String uint8_t =  "REFLECTION_uint8";
  String Uint8 =    "REFLECTION_uint8";
  String int8_t =   "REFLECTION_int8";
  String int8 =     "REFLECTION_int8";
  String float =    "REFLECTION_float";
  String double =   "REFLECTION_double";
};  

Map bytesSimpleTypes = {
  String int =      "4";
  String char =     "1";
  String uint32_t = "4";
  String uint32 =   "4";
  String int32_t =  "4";
  String int32 =    "4";
  String uint16_t = "2";
  String Uint16 =   "2";                                        
  String int16_t =  "2";
  String int16 =    "2";
  String uint8_t =  "1";
  String Uint8 =    "1";
  String int8_t =   "1";
  String int8 =     "1";
  String float =    "4";
  String double =   "8";
};  

    
sub XXXXXXXgenReflFileStruct(String dirRefl, Obj struct, String headerName) 
{
  String fileRefl = <:><&dirRefl>/<&struct.name>_crefl.c<.>;
  Openfile outRefl = fileRefl;
  <+outRefl>
  //This file is generated by zbnfjax/jzTc/GenRefl.jzTc
  #include <<&headerName>>
  #include <Jc/ReflectionJc.h>
  #include <stddef.h>
  <:call:genReflStruct:struct=struct>
  <.+close>
}


                   
sub genReflStruct(Obj struct) 
{ Num nrElements = 0;
  for(entry:struct.entries){if(entry.name) {
    nrElements = nrElements +1;
  } }
  <:>
==
==extern_C const ClassJc reflection_<&struct.name>;  //the just defined reflection_ used in the own fields.
  <.>
  for(entry:struct.entries) { if(entry.name && entry.type) { ##Note a type is not present for a #define inside a struct.
    String typeRefl;
    if(not reflSimpleTypes.get(entry.type.name)) { 
      <:>
======extern_C const ClassJc reflection_<&entry.type.name>;  //used for field <&entry.name><.>      
    }
  }}
  <:>  
==
==const struct Reflection_Fields_<&struct.name>_t
=={ ObjectArrayJc head;
==  FieldJc data[<&struct.entries.size>];
==} reflection_Fields_<&struct.name> =
=={ CONST_ObjectArrayJc(FieldJc, <&nrElements>, OBJTYPE_FieldJc, null, &reflection_Fields_<&struct.name>)
==, { <.>
  for(entry:struct.entries){ if(entry.name) {
    String typeRefl;
    String bytesType;
    String modifier;
    if(entry.type.pointer){ modifier = "kReference_Modifier_reflectJc"; }  ##reference type, from primitive or class type. 
    if(entry.macro == "OS_HandlePtr") {  ##special macro for bus - handle
      typeRefl = <:>&reflection_<&entry.type.name><.>;
      modifier = "kHandlePtr_Modifier_reflectJc | kReference_Modifier_reflectJc";
    } elsif(entry.type) {  
      bytesType = bytesSimpleTypes.get(entry.type.name);
      if(bytesType){ modifier = <:>(<&bytesType><<kBitPrimitiv_Modifier_reflectJc)<.>; } else { modifier = "0"; }
      typeRefl = reflSimpleTypes.get(entry.type.name);
      if(!typeRefl) { typeRefl = <:>&reflection_<&entry.type.name><.>; }
    } else {
      //another macro is ignored.
    }
    String arraysize;
    if(entry.arraysize.value) {
      arraysize = <:><&entry.arraysize.value> //nrofArrayElements<.>;
      modifier = <:><&modifier> | kStaticArray_Modifier_reflectJc<.>;
    } else {
      arraysize = "0   //no Array, no Bitfield";
    }
    if((entry.arraysize || not bytesType) && not entry.type.pointer && not entry.type.pointer2) {
      modifier = <:><&modifier>|kEmbeddedContainer_Modifier_reflectJc<.>;
    }
    if(entry.type.pointer) { modifier = <:><&modifier>|mReference_Modifier_reflectJc<.>; }
    if(typeRefl) { ##else: a #define
    <:><:indent:2=><: >
      { "<&entry.name>"
====    , <&arraysize>
====    , <&typeRefl>                                                                                            
====    , <&modifier> //bitModifiers
====    , (int16)((int32)(&((<&struct.name>*)(0x1000))-><&entry.name>) -(int32)(<&struct.name>*)0x1000)
====    , 0  //offsetToObjectifcBase
====    , &reflection_<&struct.name>
====    }
====  <:hasNext>, <.hasNext><: >
    <.>
    }
  } } //if for
  ##The class:
  String classModif;
  
  <:>  
==} };
==
==const ClassJc reflection_<&struct.name> =
=={ CONST_ObjectJc(OBJTYPE_ClassJc + sizeof(ClassJc), &reflection_<&struct.name>, &reflection_ClassJc)
==, "<&struct.name>"
==, 0
==, sizeof(<&struct.name>)
==, (FieldJcArray const*)&reflection_Fields_<&struct.name>  //attributes and associations
==, null  //method      
==, null  //superclass  ##TODO check first union
==, null  //interfaces  ##TODO check first union
==, 0 ##TODO |mObjectJc_Modifier_reflectJc if union{ ObjectJc obj, ...} or 1. element ObjectJc
==};
==
==<.>
}








################################################################################################################
##
##The routine to generate reflection files, one file per header file.
##It parses all header files one after another, then generates reflection
##
sub genReflection(Obj target: org.vishia.cmd.ZmakeTarget, String html = null)
{
  Obj headerTranslator = java new org.vishia.header2Reflection.CheaderParser(console);

  <+out>currdir=<&currdir><.+n>
  List inputs = target.allInputFiles();
  for(input:inputs)
  { <+out>files: <&input.absfile()><.+n>
  }
  List inputsExpanded = target.allInputFilesExpanded();
  for(input:inputsExpanded)
  { <+out><&input.absfile()><.+n>
    ##java org.vishia.util.DataAccess.debugMethod("setSrc");
    Obj args = java new org.vishia.header2Reflection.CheaderParser$Args();
    args.addSrc(input.absfile(), input.localname());
  
    args.setDst(target.output.absfile());
    
    args.setZbnfHeader(<:><&scriptdir>/../zbnf/Cheader.zbnf<.>);
    
    
    Obj headers = headerTranslator.execute(args);
  
  
    for(headerfile: headers.files){
      <+out>generate <&headerfile.fileName> <.+n>
      ##Only to help change the script, output all parsed data:
      if(html !=null) {
        test.dataHtml(headers, File:<:><&html>/<&headerfile.fileName>.html<.>);
      }
      ##
      String fileRefl = <:><&target.output.absdir()>/<&headerfile.fileName>.crefl<.>;
      call genReflectionHeader(headerfile = headerfile, fileRefl = fileRefl);
    }
  }
}










################################################################################################################
##
##The routine to generate reflection files, one file per header file.
##It parses all header files one after another, then generates reflection
##
sub genReflectionHeader(Obj headerfile, String fileRefl)
{
  Openfile outRefl = fileRefl;
    <+outRefl>
    <:>
========//This file is generated by ZBNF/zbnfjax/jzTc/Cheader2Refl.jzTc
========#include "<&headerfile.fileName>.h"  ##it comes from args.addSrc(...,input.localname())
========#include <Jc/ReflectionJc.h>
========#include <stddef.h>
    <.><.+>
  for(classC: headerfile.listClassC) {
    for(entry: classC.entries) {
      if(entry.whatisit == "structDefinition") {
        ##
        ##check whether a Bus should be generated.
        ##
        <+outRefl><:call:genReflStruct:struct=entry><.+>
      }
      if(entry.whatisit == "unionDefinition") {
        <+>  union <.n+>
        for(variant: entry.listVariantes) {
          if(variant.struct) {
            <+>  :struct <&variant.struct.tagname>  <.+n>
            
          }
        }
      }
    }
  
  }
  outRefl.close();
}



##############################################################################################################
##
## main only if it is invoked with absolute path to one headerfile.
##
main()
{
  zmake <:><&$2>.crefl<.> := genReflection($1);
}
